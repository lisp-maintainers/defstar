;;;; -*- Mode: Lisp; Syntax: ANSI-Common-Lisp; Base: 10 -*- ;;;;;;;;;;;;;;;;;80

(in-package :cl-user)


;;; Note: documentation generated with CLDOC, using the following form:
#-(cldoc:extract-documentation 'cldoc:html
      "/home/paul/lisp/defstar/html" 
      (asdf:find-system :defstar)
      :table-of-contents-title
      "DEFSTAR"
      :section-names '("Arguments:" "Returns:" "Examples:" "See Also:"
                       "See also:"
                       "Example:" "Examples:"  "Notes:" "Description:"
                       "Limitations:"
                       "Examples of DEFVAR* and DEFPARAMETER*:"
                       "Examples of DEFUN* and DEFMETHOD* usage:"
                       "Type DECLARATION versus type CHECKING:"))


(defpackage :defstar
  (:use :cl)
  (:export #:defun*
           #:defmethod*
           #:defgeneric*
           #:defvar*
           #:defparameter*
           #:flet*
           #:labels*
           #:lambda*
           #:returns
           #:return-value
           #:*check-argument-types-explicitly?*
           #:->)
  (:documentation
   " * Description:

DEFSTAR is a collection of macros that can be used in place
of DEFUN, DEFMETHOD, DEFGENERIC, DEFVAR, DEFPARAMETER, FLET, LABELS and LAMBDA. Each
macro has the same name as the form it replaces, with a star added at the
end (e.g. DEFUN*).

DEFSTAR's home is at:
- {http://bitbucket.org/eeeickythump/defstar/}

Installation requires ASDF. DEFSTAR does not depend on any other libraries.

The macros allow:
- easy inclusion of type declarations within lambda lists
- easy inclusion of return type declarations in function and method definitions
- easy inclusion of assertions for each argument and for the function's
return value

See {defmacro defstar:defun*} and {defmacro defstar:defvar*} for a detailed
description of syntax. See also the examples below.

* Type DECLARATION versus type CHECKING:

Technically, DECLARE, DECLAIM and the like do not actually check that
values stored in the associated variables conform to the declared type.
They merely constitute a promise BY THE PROGRAMMER that only values of
the specified type will be stored there. The consequences of storing
a string in a variable that is declared to be of type integer, are
technically 'undefined'.

In practice, most modern Common Lisp implementations perform type-checking
based on declaration information, especially when SAFETY is high.

DEFSTAR allows you to force lisp to perform type checking based on
declarations. If you set the global variable
{defvar defstar:*CHECK-ARGUMENT-TYPES-EXPLICITLY?*}
to non-nil, CHECK-TYPE forms will included in the body of each function
or method, causing an error to be raised if a value does not match
its declared type.

* Examples of DEFUN* and DEFMETHOD* usage:
;;; ;; Define a simple function that adds two numbers, both of which
;;; ;; are declared to be real.
;;; (defun* sum ((a real) (b real))
;;;    (+ a b))

;;; ;; Now also declare that the function returns a real.
;;; (defun* (sum -> real) ((a real) (b real))
;;;    (+ a b))

;;; ;; Another way of declaring the function's return type.
;;; (defun* sum ((a real) (b real))
;;;    (returns real)
;;;    (+ a b))

;;; ;; We want to ensure that a and b are never negative.
;;; ;; One way is to alter the type declarations:
;;; (defun* (sum -> (real 0)) ((a (real 0)) (b (real 0)))
;;;    (+ a b))

;;; ;; Another way is to define a new type:
;;; (deftype natural () '(real 0))
;;; (defun* (sum -> natural) ((a natural) (b natural))
;;;    (+ a b))

;;; ;; Another way is to use assertions:
;;; (defun* (sum -> real (>= return-value 0)) ((a real (>= a 0)) (b real (>= b 0)))
;;;    (+ a b))

;;; ;; Or:
;;; (defun* sum ((a real (>= a 0)) (b real (>= b 0)))
;;;    (returns real (>= return-value 0))
;;;    (+ a b))

;;; ;; Or, using the feature that the names of single-argument predicate
;;; ;; functions can be used as assertions:
;;; (defun* (naturalp -> boolean) ((x real))
;;;    (not (minusp x)))
;;; ...
;;; (defun* (sum -> real naturalp) ((a real naturalp) (b real naturalp))
;;;    (+ a b))

;;; ;; A function that returns multiple values.
;;; (defun* (floor -> (values integer integer)) ((n real) (d real))
;;;    (cl:floor n d))

;;; ;; It is possible to use assertions with functions that return
;;; ;; multiple values. When a function is declared to return multiple
;;; ;; values, RETURN-VALUE will be bound to a LIST of those values.
;;; (defun* floor ((n real) (d real))
;;;    (returns (values integer integer)
;;;             (< (second return-value) (first return-value)))
;;;    (cl:floor n d))

;;; ;; To declare that a function returns an unspecified number of
;;; ;; values, of unspecified types:
;;; (defun* (floor -> (values)) ((n real) (d real))
;;;    ...)

;;; ;; The type of a &REST argument can be declared. The declaration
;;; ;; refers to the types of each element in the list of arguments
;;; ;; stored in the &REST argument.
;;; (defun* (+ -> real) (&rest (numbers real))
;;;    (apply #'cl:+ numbers))

;;; ;; More complicated lambda list.
;;; ;; Note that the function and its first argument do not have type
;;; ;; declarations.
;;; ;; Also note the syntax of typed keyword arguments:
;;; ;; ((var TYPE [ASSERTION]) DEFAULT [SUPPLIEDP])
;;; ;; Note that &OPTIONAL arguments use the same syntax.
;;; (defun* my-find (item (seq sequence) &key (from-end boolean)
;;;                  ((test (function (t))) nil) ((test-not (function (t))) nil)
;;;                  ((key (function (t))) nil) (start fixnum) (end fixnum))
;;;    ...function body...)

;;; ;; Example of method definition. All the arguments in the arglist are
;;; ;; normal 'specialised' arguments like you would usually find in a
;;; ;; method definition. The form still allows you to include an assertion
;;; ;; with each argument, however ('plusp' in this case).
;;; (defmethod* (cell-value -> real) :around ((sheet <Sheet>)
;;;                                           (x integer plusp) (y integer plusp))
;;;    ...)

;;; ;; Note that when you declare a return type for a method, the method
;;; ;; body will perform type-checking, but no toplevel DECLAIM form will
;;; ;; be generated.

;;; ;; CLOS function dispatch based on classes is limited; you cannot specialise
;;; ;; on user-defined types unless they are proper classes, for example.
;;; ;; You may therefore sometimes want to declare that a method's argument
;;; ;; is of a particular type, as well as declaring its class for specialisation
;;; ;; as you normally would.
;;; ;; Here is an example. Note the similarity to the syntax for keyword
;;; ;; arguments.
;;; (defmethod* (cell-value -> real) :around ((sheet <Sheet>)
;;;                                           ((x natural plusp) integer)
;;;                                           ((y natural plusp) integer))
;;;    ...)

;;; ;; Example of DEFGENERIC*, mainly useful to declare the return type
;;; ;; of a set of methods.
;;; (defgeneric* (cell-value -> real) (sheet x y))

;;; ;; DEFGENERIC* can also be used to declare types of arguments. Be careful
;;; ;; that these don't clash with specialisers in method definitions.
;;; (defgeneric* (cell-value -> real) (sheet (x natural) (y natural)))

* Examples of DEFVAR* and DEFPARAMETER*:
;;; (defvar* (*user-name* string) \"Bob\")
;;; (defparameter* (*file-position* (integer 0)) 0)

* Limitations:
- Definitions of SETF methods cannot include return type declarations in the
method 'header'. The return type can still be declared using a (RETURNS ...)
form. For example:
;;; (defmethod (setf (foo -> integer)) (...args...)   ; illegal
;;;    ...)

;;; (defmethod (setf foo) (...args...)
;;;    (returns integer)                  ; legal
;;;    ...)
"))

(in-package :defstar)


(declaim (optimize (speed 0) (safety 3) (debug 3)))


;;;; (@> "Utility functions") =================================================


(eval-when (:compile-toplevel :load-toplevel :execute)
  (defun defstar/split-defun-body (body)
    "* Arguments:
- BODY: the body of a DEFUN form or similar, such as might be received
by a macro.

* Returns:
Three values:
- PREAMBLE: list of declaration forms at the start of the body
- DOCSTRING: Documentation string, if present in BODY.
- TRUE-BODY: Actual function body with the above items removed.

* Description:
Internal utility function.
Divide the 'preamble' of a function body from its actual body.
The preamble consists of declarations and a docstring."
    (let ((docstring nil)
          (preamble nil)
          (true-body nil))
      (loop for form on body do
           (cond
             ((and (listp (car form))
                   (member (caar form) '(declare declaim proclaim)))
              (push (car form) preamble))
             ((and (stringp (car form))
                   (null docstring)
                   (cdr form))
              (setf docstring (car form)))
             (t
              (setf true-body form)
              (return))))
      (values (reverse preamble) docstring true-body))))


(eval-when (:compile-toplevel :load-toplevel :execute)
  (defun defstar/make-keyword (&rest parts)
    "Like MKSYMBOL, but intern the symbol in the KEYWORD package,
creating a keyword called :SYMBOL."
    (intern (string-upcase (format nil "窿疳螋螬弘妁黠蜾┅ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐躅溴骟翎虔犴疱蝮犷洵簌礅镬簌愆⑿蝈溟汜翦娘弩翳簌礅镬淤忮玳鏖翳犷犴疱蝮犷洮篚汨狍α钨σ庞犷箫镱竣ㄡ钿簌礅镬簌愆ㄥ耢＼ㄣ栳ㄦ矧磲铋立簌愆癌┅┅换换谰⑸铘弪钺骢钽糸镱犷磲泸矬浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐泔铙翎铘呐普为烈蚁篆淤吐咸Л⒃桢簌礅镬翳狒箦疳蜥翦骢钽糸镱钺礤骝镯豉疱溴沆狎狒轱轭呐普为骘蝽犷翳扉脲渝溴骓徙蝻溴骟翎蚝溴骢瞠ㄤ彐鲠汨邈氕狎珲礤铘豉疱蟓屮痨殂轸禊开铋腻筱蜷痿轱詈涉铒瞽铋飕轭箦螋萌琶谁再信沆狨箦轭翳痱遽礅戾镦骢钽糸镱蟋麸骘蜚翳骢钽糸镱狎珲礤铘麸忮豉疱汨邈脲洚藻汨铋汜祆呐锰烈努呐锰辽犷翳扉脲滹铒徙趱犰禊汨邈翳狒鲠祯弩篝矧邃轭翳狍箫汩狒邃鲠蜷徕戾泔铈矧麸翳溴沆狎邃豉疱澡妁礤蝈禊泔铙糸趱翦痱镯轶沦匀幸锨伊屯乓翳狒镱禊鲠祯弩镦翳箴邈殒殄豉疱鏖祆忮篝矧邃翳弪瀹澡泔铙羼蹂钽弩镦篝矧轭篝蜷铉轭鲠蜷徕戾翳狒轶溴沆狎邃麸忮镦豉疱轭翦珏颥狎躅溴骈铄洚深痱徙糸箦弩箦铘獒祆犰盹溴蝾扉箴滹疱蜴矧豉疱汨邈腴铉忉箦镱溴沆狎狒轱铙弩疱汩犰禊麒孱翳恿婆再箦趑轭轶栝玷ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐躅溴骢瞠翦蝽翦蝽灬篝犴瓠膑脲ㄤ彐豉疱т彐躅┅硫珲礤铘蠛耘彝犷礤礅弪镦犷矧溟钺蝙灬礅溽扉篝塘釉镣协俗暮御礅镬矧铋飚呐骗再信御礅镬溴铒糸铉翳豉疱镦麸痨弼屐骘蝽翳狒轶忮轭泸遽翦洚澡溴驷蹯轶呐普萎义趱蝾蠛骑躜鲠祯弩碑澡翦蝽狍轸箬秕熹忮轭沆蹁邃轭翳骈钺狎珲礤铘扉篝骘翳麸痨弼屐骘蝽伯澡溴沆狎狒轱沆狨箦翳狒箬秕熹忮轭沆蹁邃轭翳呐锰烈篝狒屙孱鏖翳轭翳麸痨弼屐骘蝽怙澌钞澡豉疱镦翳翦蝽骘轭沆躞轱轭翳狎珲礤铘扉篝镦翳呐锰辽ㄆ再信ㄆ瘴迷上狎珈轶遗哉椅再信┅骘蝽骘骢钽糸镱溴骈铋糸镱串澡狍箦螋轱沆狨箦腻筱蜷痿轱詈深翦蝾犰骢钽糸镱躞邃怡溴骓徙蝻溴骟翎蚝溴骢瞠麸疳蝮灬礅溽扉篝翦蝽螽渝犰箫溴骓徙蝻溴骟翎蚝溴骢瞠ㄦ戾è汨邈氕沆狨箦ㄣ桢汶鲠颟ㄩㄡ钿汨邈簌礅镬汨邈氅扉篝汨邈鲠颟汨邈氅┅ㄣ镱è铛祆灬篝犴瓠膑洎ㄣ镱è扉篝翦蝽ㄣ镱è犷ㄥ耢т彐礤翳镤溴姝豉疱扉篝ㄣ狎翦蝽┅ㄤ弩趄蹉趱蜷铉忾钿è鲠鲠螋疱镳糸镱犰汨邈氅鲠蜚灬篌翦蝽鲠祯弩扉篝鲠鲠蜚灬篌扉篝豉疱鲠螋疱鲠颟鲠蜚灬篌ㄣ桢汶沆狨箦汨邈鲠颟┅è羼т彐礤翳镤溴姝豉疱ㄤ弩趄蹉趱蜷铉忾钿鲠鲠蜚灬篌镳糸镱犰汨邈氅翦蝽鲠祯弩扉篝鲠鲠蜚灬篌铋鲠蜚灬篌ㄣ桢汶沆狨箦汨邈鲠颟┅è羼т彐珏铄蜷溴姝豉疱ㄤ弩趄蹉趱蜷铉忾钿鲠鲠螋疱翦蝽鲠祯弩鲠扉篝豉疱鲠螋疱鲠颟鲠螋疱铋飑┅ㄤ弩趄蹉趱蜷铉忾钿鲠鲠螋疱镳糸镱犰汨邈氅翦蝽鲠祯弩鲠扉篝豉疱鲠螋疱鲠颟鲠螋疱ㄣ桢汶沆狨箦汨邈鲠颟┅┅鲠祯弩翦蝽铋铋飑┅è羼Е蝈篝灬篝犴瓠膑洎ㄣ镱è扉篝翦蝽ㄤ弩趄蹉趱蜷铉忾钿鲠鲠螋疱镳糸镱犰汨邈氅翦蝽鲠祯弩鲠铋鲠螋疱ㄣ桢汶沆狨箦汨邈鲠颟┅鲠祯弩翦蝽铋铋飑┅è矧ㄥ耢Е镳糸镱犰灬篝犴瓠膑洎ㄥ耢Е脲灬篝犴瓠膑洎ㄣ镱è犷扉篝翦蝽ㄥ耢т彐珏铄蜷溴姝豉疱┅ㄤ弩趄蹉趱蜷铉忾钿鲠鲠螋疱翦蝽鲠祯弩鲠铋ㄩㄥ耢Е脲灬篝犴瓠膑洎扉篝ㄤ彐篝狎磲脲脲黠蜾鲠颟鲠螋疱鲠螋疱铋飑┅è犷扉篝翦蝽扉篝ㄣ狎翦蝽┅ㄤ弩趄蹉趱蜷铉忾钿è鲠鲠螋疱镳糸镱犰汨邈氅溴驷蹯镳糸镱犰篚痧扉邃皓翦蝽鲠祯弩ㄩ篚痧扉邃扉篝鲠溴驷蹯篚痧扉邃皓扉篝鲠溴驷蹯舂扉篝豉疱鲠螋疱鲠颟ㄩㄥ耢Е脲灬篝犴瓠膑洎扉篝ㄤ彐篝狎磲脲脲黠蜾鲠颟鲠螋疱鲠螋疱ㄣ桢汶沆狨箦汨邈鲠颟┅è扉篝翦蝽鲠祯弩翦蝽铋ㄩㄥ耢Е脲灬篝犴瓠膑洎扉篝ㄤ彐篝狎磲脲脲黠蜾ㄣ狎翦蝽┅舂舂铋飑鲠祯弩翦蝽铋ㄩㄥ耢Е脲灬篝犴瓠膑洎扉篝ㄤ彐篝狎磲脲脲黠蜾翦蝽舂舂铋飑┅è羼Е狨灬篝犴瓠膑洎ㄣ镱è犷扉篝翦蝽扉篝ㄣ狎翦蝽┅ㄤ弩趄蹉趱蜷铉忾钿è鲠鲠螋疱镳糸镱犰汨邈氅溴驷蹯舂翦蝽鲠祯弩扉篝鲠溴驷蹯舂扉篝豉疱鲠螋疱鲠颟铋ㄣ桢汶沆狨箦汨邈鲠颟┅鲠祯弩翦蝽铋铋铋飑┅è羼Е犰祜鳝雉桢颦脲灬篝犴瓠膑洎ㄥ蝌矧⑼犰骘蝽邃灬礅溽扉篝α烫献显扰噎伺儆眭篝忮灬篝翦蝽┅ㄥ蝌矧⒄铍铒黝脲黠蜾轭灬礅溽扉篝英灬篝犴瓠膑洎┅┅ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐躅筢驽溴骈铄麸痨弼屐骘蝽钺礤骖犴狎珈轶怙澌硫珲礤铘蠛韵刑胖盘葡彝瘟团御礅镬溴铒糸铉翳豉疱镦麸痨弼屐骘蝽忮轭溴骈铄洚悯蝌孱綮栳钿戾呐普维呐仆旁认默铺旁塘屡逃塘吐牧呐魄盼乓擅莆镣努烈翘捎袁孪馁箦呐普为义趱蝾蠛噤彐躅К噤彐礤翳镤К噤彐珏铄蜷悃矧囔犴怃岌骘蝽矧噫戾臾矧囔徕屐螫篚忏灬躞瀣鏖翳狃痱镳蜷狒溴沆狎狒轱铙腻筱蜷痿轱詈深翦蝾犰骢钽糸镱澡黠螂栾蝮骘翳磲泸矬呐普为呐仆旁认莫塘吐牧铺旁犷塘屡逃戾è礤翳镤泔礅锃脲黠蜾铋飑ㄦ矧憝狎珞铋飑ㄦ豉疱狎珞铋飑ㄤ邈灬蜥糸镱铋飑ㄣ桢汶铋飑蝈趱蝾蟓沆狨箦ㄦ轭蝈趱蝾怙澌弘妁＇灬礅溽ㄩ扉篝ㄣ狎┅┅蝈趱蝾蟓豉疱舂蝈趱蝾蟓汨邈铋飑ㄦ轭犰骘蝽铋飑ㄡ眇铋飑麒孱ㄡ钿ㄥ耢т彐礤翳镤麸痨弼屐骘蝽钺礤铒扉篝狎珈轶舂┅换砒趄徙翳聃犰殒殄蝮ㄥ毫圃乓毫蚁瘴弭悌瘐箬狎珈轶礤翳镤泔礅锃脲黠蜾螬祜镳骘翦蝽轭怙澌麒殪铒扉篝翦蝽┅滹瘐箬翦蝽礤翳镤泔礅锃脲黠蜾螬痫怙澌┅箦翩狎珈轶ㄣ狎怙澌┅箦翩怙澌ㄣ潋怙澌┅ㄤ镬轶翦蝽狎珈轶舂ㄣ镱è溴骟翎虔犴疱蝮犷洵簌礅镬翦蝽箦翩犴翦蝽瘐箬翦蝽骘蝽狎珞躅戾篌ㄥ耢Е狨翦蝽瘐箬翦蝽骠疱狎珞┅眭祠轲戾鲠祯瀛忾钿ㄦ矧憝翦蝽溴沆骠疱翦蝽汨邈氅ㄤ彐躅翦蝽翦蝽犴轰彐豉疱麸痨弼屐骘蝽钺礤瘐箬骘蝽翦蝽骘蝽狎珞ㄩ溴沆瘐箬溴沆溴沆狎狒轱铙┅ㄩ骠疱翦蝽瘐箬骠疱翦蝽骠疱狎珞┅ㄩ汨邈瘐箬汨邈汨邈塍┅┅┅麒孱蝈趱蝾蟓沆狨箦ㄤ弩趄蹉趱蜷铉忾钿螋疱镳糸镱犰蜚桢汶ㄣ潋蝈趱蝾蟓沆狨箦箦翩蝈趱蝾蟓豉疱螋疱ㄩ蜚桢汶箦翩蝈趱蝾蟓汨邈蜚桢汶┅ㄩㄡ钿蜚桢汶簌礅镬蜚桢汶┅箦翩蝈趱蝾蟓汨邈啜蜚桢汶蝈趱蝾鲠祯濠┅箦翩怙澌蝈盹鲥蝈趱蝾蟓沆狨箦怙澌呼弩＇羼飑┅麒孱ㄡ钿骖犴扉篝骖犴濠铒ㄥ耢箦翩ㄣ狎骖犴濠┅麒孱蝈趱蝾蟓沆狨箦ㄥ蝌矧⒛牌瘴犰箫泔铘衢铙б旁找斡沆狨箦轭怙澌骖犴濠ㄤ弩趄蹉趱蜷铉忾钿ㄦ躅钺礤狎蝻螋疱镳糸镱犰蜚桢汶骖犴躅戾篌ㄥ耢狎蝻呐普为烈蚁篆淤吐咸ㄥ蝌矧⑼犰骘蝽邃呐普为桢徜弪骘躅蝇屮疱泗邃狎蝻簌礅镬萤狎蝻呐普为烈蚁篆淤吐咸┅箦翩骖犴骢瞽钺礤箦翩蝈趱蝾蟓豉疱螋疱ㄩ蜚桢汶箦翩蝈趱蝾蟓汨邈蜚桢汶┅ㄩㄡ钿蜚桢汶簌礅镬蜚桢汶┅箦翩蝈趱蝾蟓汨邈啜蜚桢汶蝈趱蝾鲠祯濠┅┅眭祠轲戾鲠祯瀛忾钿痱遽礅戾滹泱趄轭趄蹂怙澌ㄤ彐篝狎箴扉舡溴骢瞽怙澌怙澌箦翩痱遽礅戾啜括殒溴沆狎狒轱铙啜ㄤ邈灬蝈冷邈灬蜥糸镱螬铋飑鲤蝈犴忪濠ㄩ汨邈氕狎珲礤铘豉疱蟓屮痨殂轸禊开箦翩痱遽礅戾ㄡ痧孱痱遽礅戾磲疸狎＇灬礅溽ㄤ邈飑啜汨邈氕豉疱翳轵溴沆箦泔钿溴沆┅溴沆狎狒轱铙┅┅箦翩趄蹂怙澌啜括殒ㄡ钿汨邈塍铒ㄥ耢т彐珏铄蜷麸痨弼屐骘蝽钺礤┅磲疸狎＇灬礅溽ㄣ桢汶啜狍箦螋汨邈氅汨邈塍铋飑吏蝓瀛怙澌┅箦翩骘蝽狎珞蝈鲥蝮骘蝽狎珞骠疱狎珞蝈鲥蝮骠疱狎珞汨邈塍蝈鲥蝮汨邈塍┅ㄩㄡ钿蝈趱蝾蟓汨邈簌礅镬蝈趱蝾蟓汨邈氅箦翩蝈趱蝾蟓汨邈啜蝈趱蝾蟓汨邈蝈趱蝾鲠祯濠┅箦翩骈钺飙骘蝽啜麸痨弼屐骘蝽钺礤括殒骖犴扉篝骖犴濠铋飑理弭栾洵泔礅锃脲黠蜾骘蝽狎珞括殒滹泱趄轭扉篝滹泱趄轭绌铋飑括殒ㄥ耢т彐珏铄蜷麸痨弼屐骘蝽钺礤铋痱遽礅戾括泔钿è羼т彐珏铄蜷麸痨弼屐骘蝽钺礤趄蹂怙澌è犷蝈趱蝾蟓汨邈扉篝蝈趱蝾蟓豉疱ㄥ鲠祯弩ㄣ狎蝈趱蝾蟓豉疱┅啜翳蝈趱蝾蟓豉疱戾è蝈趱蝾鲠祯眭祠轲戾鲠祯瀛扉篝痱镧吏蝓瀛怙澌┅┅ㄡ篌弪蝈趱蝾蟓汨邈氅鲠祯弩扉篝蝈趱蝾鲠祯濠┅┅蝈趱蝾蟓汨邈啜翳蝈趱蝾蟓豉疱戾è蝈趱蝾鲠祯痱镧吏蝓瀛怙澌┅ㄡ篌弪蝈趱蝾蟓汨邈氅蝈趱蝾鲠祯濠┅蝈趱蝾蟓豉疱啜翳蝈趱蝾蟓豉疱ㄩㄣ潋趄蹂怙澌啜痱镧吏蝓瀛怙澌ㄣ狎趄蹂怙澌┅┅趄蹂怙澌┅┅ㄣ镱è犷矧溴沆狎狒轱铙蝈趱蝾蟓豉疱铒礤礅弪麸痨弼屐骘蝽钺礤Ж溴骓弭栾骒弭灬忮祗灬礅溽┅┅啜痱镧ㄤ邈灬轫ㄦ豉疱ㄦ躅泗轱骠疱狎珞蝈趱蝾蟓豉疱骖犴濠骈钺飙骘蝽┅è礤礅弪麸痨弼屐骘蝽钺礤Ж骒弭灬忮祗┅ㄣ潋骈钺飙骘蝽┅骈钺飙骘蝽┅┅┅ㄤ彐磲泸溴骣狎疳蜥麸痨弼屐骘蝽钺礤鲠鲠祯镳糸镱犰滹泱趄轭绌⑸铘弪钺磲泸铿躞邃怡溴骓徙蝻溴骟翎蚝溴骣狎犷溴骓徙蝻溴骟翎蚝溴骛狎犴弭弪ㄣ镱è扉篝鲠颟ㄤ弩趄蹉趱蜷铉忾钿鲠蝾犴鲠螋疱鲠啜痱镧ㄤ邈灬轫豉疱鲠螋疱鲠蝾犴濠ì麸痨弼屐骘蝽钺礤鲠蝾犴鲠祯括殒滹泱趄轭扉篝滹泱趄轭绌铋飑┅┅啜麸痨弼屐骘蝽钺礤鲠鲠祯括殒滹泱趄轭扉篝滹泱趄轭绌铋飑┅┅换换谰⑴痫螋邃磲泸矬浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇浇ㄤ彐磲泸溴骢瞠ㄦ钺礤狎珈轶怙澌怙澌硫珲礤铘蠛莆镣藕彘翳弪翳钺礤镦翳骢钽糸镱麸忮泸遽翦洮矧扉篝狍箴邈殒殄忮祜骱换骖犴普蚊陨衔瘟团换ㄆ瘴迷上苇瘟团再信坩篌弪糸镱荸换狍箦螋轱葡彝换幸拍擅猎怒淤吐咸阻弪搴再信轶犷鲠扉豉疱箴邈殒殄葡彝轶犷骘蝽麒殂眭篝蝈趱蝾铒瞽铋殒翳狍箦螋轱轶筢糸箧殄洮铋雉桢蝼轶瀹组翳轭翳骘蝽翳簌礅镬遗哉椅至陶轶怙躅麸翳鲠祯翳狒轶徕秕麸忮蝈趱蝾邃怡翳骢钽糸镱幸拍擅猎怒淤吐咸轶簌礅镬翳钺礤镦骢钽糸镱翳狒徙沐痿箝铉戾狎珲礤铘螽篷蹰鲠戾铘麸翳骘蝽啜幸拍擅猎怒淤吐咸遗哉椅至陶农М物翦翳狒殒翳灬趑弪扉篝骘蝽骘骖犴轶躞邃翳呐普为怙澌磲蜗犰箫泔铘衢囹弭躜铙骘蝽领箫铒翦翳狒翳灬趑弪骘蝽汜铑雉沲蝌孱綮忮躞邃麒孱溴骈铋铉啜箦翩┃骢钽糸镱矧礤翳镤烈翘捎院呐普为塘吐牧躺釉麒殂翎脲翳骘蝽换狎珈轶鲠颦翦蝽换鲠颦翦蝽郐镳糸镱犰镳舡翦蝽荸换鲠颦翦蝽郐脲镳舡翦蝽荸换鲠颦翦蝽郐蝈篝蝈篝翦蝽荸换鲠颦翦蝽至椅镣换ㄖ烈瘟团再信锰劣坩篌弪糸镱荸换蝈篝翦蝽至椅镣换ㄖ烈瘟团盘磐盼原再信换镳舡翦蝽至椅镣换鲠颦翦蝽呐屏仗塾招刑膳男荸孪馁嘛澌镦翳骢钽糸镱骘蝽澡轶磲泔铘衢滹泱趄轭轭翳躞踽痨徙瀣犷磲犰箫箝铉戾箴邈獒骘蝽忮玳铑轭鏖翳囹弭躜铙Ш换蝈趱蝾蟓骘蝽ㄒ旁找斡再信坩篌弪糸镱荸涉翳囹弭躜铙骘蝽泔铘衢铙犷狍箦螋轱瞵翳孱鏖翳轭翳狒狍箦螋轱瞵翳簌礅镬嘁旁找苇至陶脓轶怙躅麸翳鲠祯翳狒翳骢钽糸镱轶徕秕麸蝈趱蝾腻筱蜷痿轱詈篷蹰鲠戾铘麸呐普骖犴狎珈轶怙澌┈怩艉领豉疱溴沆狎狒轱铙鏖翳轭翳灬礅溽扉篝鏖祆忮趱蝾邃轭麸呐锰烈女骘蝽鏖翳轭翳骢钽糸镱怙澌涉蝈趱蝾豉疱轶溴沆狎邃骘翳骢钽糸镱轸箦戽翳轶鏖祆忮趱蝾邃轭麸珈镡犰呐锰辽骘蝽翳狒痱邈邃弩翳骢钽糸镱领狍箦螋轱铙鏖翳轭翳灬礅溽扉篝鏖祆忮汨邈脲忮骘蝈翳骢钽糸镱怙澌轶孱翦蝈洚令狍箦螋轱鏖翳轭囹弭躜铙骘蝽鏖祆忮汨邈脲忮骘蝈翳骢钽糸镱蝈趱蝾鲠祯瀹砒犴痨弩换换皱蝙箝眇戾屮犴痨换ㄤ彐躅ㄡ滗蝈犰è蝈犰ㄢ蝈犰┅换ǐ猢换换砒犴痨鏖翳狍箦螋轱骘р狎珲礤铘汨邈脲忮骘蝈翳换换怙澌镦翳骢钽糸镱轶孱翦蝈洚换ㄤ彐躅溟è蝈犰ㄢ蝈犰ǒ癌┅换蝈趱蝾蝈犰换ǒ猢换ㄤ彐躅篚é蝈篝铛眢蝈犰┅豉疱镦蝈篝鲠蝈驽蝮麸换蝈趱蝾蝈犰翳豉疱镦遽汨扉篝屐屙孱衄殄换ㄡ痧禊＇铛眢┅铛眢眭篝忮扉篝镦遗撂换ㄤ彐躅篚蝈犰é蝈篝铛眢蝈犰┅犰翦蝾狒轹骘蝽换ㄡ痧禊＇铛眢┅骘徕秭屮犴痨换换澡轶骢钽糸镱犷骈蝮狎珲礤铘栳鲥铒豉疱溴沆狎狒轱铙换换隋黠蜾狎珲礤铘翦篝徙沐痿骢钽糸镱翳狒翎脲换换赭狎珲礤铘镦犷豉疱换ㄤ彐躅骈钿轭趄邋ㄩ翦趄邋泔铙换脲è翦篝ㄦ躅泗轱舂┅＇羼踽飑换矧ㄦ躅汜祆翦篝轸屙趄邋换ㄡ钿ㄣ镱箴趄邋换矧ㄦ轭洵轭趄邋轸屙ㄣ狎趄邋┅换ㄦ轭洵轭趄邋轸屙ㄣ潋趄邋┅┅┅筢驽溴骈铄т彐躅骖犴狎珈轶怙澌┅ㄤ彐磲泸溴骓弭栾洫ㄦ钺礤礤翳镤狎珈轶怙澌怙澌硫珲礤铘蠛阵徵轶屮徙綮翳筢礤狍溴骓徙蝻溴骟翎蚝溴骢瞠屮沐痿翳狒鏖翳轭团匀夏烈翘捎袁犷扉篝轭铒瞽镳糸镱犰痫箝糸镱轶狍篚礤麸忮箴邈獒扉箦灬礅溽扉篝翦蝽镦翳骘蝽ㄖ烈瘟团锰劣坩篌弪糸镱荸蜥翳弪翳犷呐普为豉疱溴沆狎轭翦蝽澡簌铘狲镦团匀夏烈翘捎轶翳弪彐矧搴换狎珈轶礤翳镤翦蝽换礤翳镤翦蝽郐镳糸镱犰镳舡翦蝽荸换礤翳镤翦蝽郐脲镳舡翦蝽荸换礤翳镤翦蝽郐蝈篝蝈篝翦蝽荸换礤翳镤翦蝽至椅镣换ㄖ烈瘟团锰劣坩篌弪糸镱荸换è至椅镣再信锰劣坩篌弪糸镱荸锰劣萤澡蝈篝镦翳簌铘狲轶翳筢礤狍骘呐普为腻筱蜷痿轱詈篷蹰鲠戾铘麸呐仆旁认莆镣团匀夏烈翘捎怙澌鏖翳豉疱溴沆狎狒轱铙犷狍箦螋轱铙狍疱溴骓徙蝻溴骟翎蚝溴骢瞠砒犴痨弩换ㄤ彐豉疱痫箝糸鲥轭翦珏ī啜轭翦珏暴换ㄤ彐礤翳镤磲脲泔矧潴ㄣ镱痫箝糸鲥轭翦珏痫箝糸鲥轭翦珏颟换è痫箝糸鲥轭翦珏颟轭翦珏颟换è痫箝糸鲥轭翦珏颟轭翦珏颟换ㄣ镱┅筢驽溴骈铄т彐礤翳镤骖犴礤翳镤狎珈轶怙澌┅ㄤ彐磲泸溴骁孱弪殂ㄦ钺礤珏铄蜷悱狎珈轶怙澌镳糸镱螬硫珲礤铘蠛阵徵轶屮徙綮翳筢礤狍溴骓徙蝻溴骟翎蚝溴骢瞠屮沐痿翳狒馏箦螋轱铙狎殓铒蝈洚σ庞袁λ刨犷ο性上瘟狎珲礤铘眭篝忮镦翳骘蝽换狎至椅镣换ㄖ烈瘟团再信砒犴痨弩换ㄤ彐珏铄蜷悛戾铉翳轭翦珏颟箦脲篝狎舂镳糸镱螽换换物翦翳狒秕汜溴沆狎豉疱骘狎珲礤铘轭翳珏铄蜷骢钽糸镱换换狎珲礤铘扉篝洛汜蝈骢翳狒翳弩滹铒沆狍鏖翳礤翳镤溴骈铋糸镱螽换换赠疱溴沆狎狒轱铙骘珏铄蜷骢钽糸镱狎珲礤铘鏖祆镱禊忮躞邃麸换换磲脲麸痨弼屐呐锰辽骘蝽翳狒鏖祆翳孱狃痨麸犰礤翳镤镦换换翳珏铄蜷骢钽糸镱换ㄤ彐珏铄蜷悛戾铉翳轭翦珏颟è箦箦聃孱沐脲篝狎轭翦珏颟换镳糸镱螽筢驽溴骈铄т彐珏铄蜷骖犴珏铄蜷悱狎珈轶镳糸镱螬ㄤ彐磲泸溴骣狎鲠鲠祯镳糸镱犰滹泱趄轭绌硫珲礤铘蠛至汜忮彘翳弪碑鲠蜷徕戾钺礤轭翳轶汜箦呐浦烈栳屮徙綮翳筢礤彐驽泗狍呐浦烈伯ㄖ烈瘟团再信麒弪至椅镣轶鲠蜷徕戾钺礤犷再信轶豉疱溴沆狎狒轱町至陶藕骘蝽麒殂轶弼犰踽翦麒孱翳鲠蜷徕戾轶骈蝮泸遽翦洚南糜砸晌呛娘沲礤铘狒轱町义趱蝾蠛澡钺礤镦翳鲠蜷徕戾狍簌礅镬腻筱蜷痿轱詈抿遽翦翳珈镡犰箴邈獒鲠蜷徕戾至椰轭轸獒扉箦轸麸至陶努犷溴沆狎弩轸麸忮镦豉疱再信殒玳鲥町砒犴痨弩换ㄤ彐鲠颡í骈戾钺礤篝蜷铉堍祜绠豇糗啜溴骣狎疳蜥溴骣狎鲠鲠祯滹泱趄轭绌ㄤ彐磲泸溴骛狎犴弭弪鲠鲠祯镳糸镱犰滹泱趄轭绌⑻殡溴骓徙蝻溴骟翎蚝溴骣狎怩羼蹰鲠戾铘麸呐菩烈镣旁乓蜥翳弪翳犷呐浦烈渝溴骓徙蝻溴骟翎蚝溴骣狎骘躞徵瀹啜溴骣狎疳蜥溴骛狎犴弭弪鲠鲠祯滹泱趄轭绌ㄤ彐磲泸骒弭ㄣ灬躞弩怙澌怙澌腻筱蜷痿轱詈涕脲铺旁怩鏖翳轭遽汨沆狨箦翳骢钽糸镱钺礤狎珈轶犷怙澌栳鲥翳筢礤簌铘狲狍骘溴骓徙蝻溴骟翎蚝溴骢瞠啜骒弭磲疸狎＇灬礅溽ㄣ灬躞濠ㄤ弩趄蹉趱蜷铉忾钿ㄦ钺礤狎珈轶蝈篝沆狨箦怙澌沆狨箦筢驽溴骈铄ф戾骖犴狎珈轶沆狨箦怙澌┅沆狨箦螬棱镤┅ㄤ彐磲泸灬忮祗ㄣ灬躞弩怙澌怙澌腻筱蜷痿轱詈涕脲塘屡逃怩鏖翳轭遽汨沆狨箦翳骢钽糸镱钺礤狎珈轶犷怙澌栳鲥翳筢礤簌铘狲狍骘溴骓徙蝻溴骟翎蚝溴骢瞠啜灬忮祗磲疸狎＇灬礅溽ㄣ灬躞濠ㄤ弩趄蹉趱蜷铉忾钿ㄦ钺礤狎珈轶蝈篝沆狨箦怙澌沆狨箦筢驽溴骈铄ъ徕屐骖犴狎珈轶沆狨箦怙澌┅沆狨箦螬棱镤┅ㄤ彐磲泸灬礅溽ㄡ蜱扉篝怙澌怙澌腻筱蜷痿轱詈涕脲塘吐牧怩翳狎珈轶犷怙澌栳鲥翳筢礤簌铘狲狍骘溴骓徙蝻溴骟翎蚝溴骢瞠囹弭躜铙骘蝽汜忮躞邃鏖翳轭翳骢钽糸镱怙澌麸溴沆狎轸蝈趱蝾豉疱筢驽溴骈铄ъ犴怃铋狎珈轶怙澌┅换换蓬镦呐朴粤